<?xml version = "1.0" encoding = "utf-8"?>
<!--
Copyright 2021 Brightworks, Inc.

This file is part of Language Mentor.

Language Mentor is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Language Mentor is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Language Mentor.  If not, see <http://www.gnu.org/licenses/>.
-->
<view:View_Base xmlns:fx = "http://ns.adobe.com/mxml/2009"
                xmlns:list = "com.brightworks.component.list.*"
                xmlns:view = "com.langcollab.languagementor.view.*"
                implements = "com.brightworks.interfaces.IDisposable"
                title = "Manage Lessons"
                viewActivate = "onViewActivate()">

   <fx:Script>
        <![CDATA[
      import com.brightworks.util.Log;
      import com.brightworks.util.Utils_ANEs;
      import com.brightworks.util.Utils_Dispose;
      import com.langcollab.languagementor.util.Utils_LangCollab;
      import com.langcollab.languagementor.view.supportClasses.ViewContext;

      import mx.collections.ArrayCollection;

      private static const _LIST_OPTION__DELETE_I_KNOW_THIS_DATA:String = 'Delete "I Know This" Data';
      private static const _LIST_OPTION__DESELECT_ALL_LESSONS:String = "Deselect All Lessons";
      private static const _LIST_OPTION__SELECT_LESSONS:String = "Select Lessons";
      private static const _LIST_OPTION__SELECT_LESSON_LEVELS:String = "Select Lesson Levels";

      private var _isDisposed:Boolean = false;
      private var _listDataProvider:ArrayCollection;

      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      //
      //          Public Methods
      //
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

      override public function dispose():void {
         super.dispose();
         if (_isDisposed)
            return;
         _isDisposed = true;
         if (_listDataProvider) {
            Utils_Dispose.disposeArrayCollection(_listDataProvider, true);
            _listDataProvider = null;
         }
      }

      override public function doGoBack():void {
         Log.info("View_ManageLessons.doGoBack()");
         navigator.pushView(View_More, null, null, transition_SlideView_Right);
      }

      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      //
      //          Private Methods
      //
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

      private function onListChange():void {
         Log.info("View_ManageLessons.onListChange(): " + list.selectedItem);
         var ctxt:ViewContext;
         switch (list.selectedItem) {
            case _LIST_OPTION__DELETE_I_KNOW_THIS_DATA: {
               navigator.pushView(View_DeleteIKnowThisData, null, null, transition_SlideView_Left);
               break;
            }
            case _LIST_OPTION__DESELECT_ALL_LESSONS: {
               if (model.getLessonVersionCount() < 1) {
                  // No downloaded lessons
                  Utils_ANEs.showAlert_OkayButton("No lessons are currently selected."); /// Localize
                  return;
               }
               else if (currentLessons.length == 0) {
                  // No selected lessons
                  Utils_ANEs.showAlert_OkayButton("No lessons are currently selected."); /// Localize
                  return;
               }
               navigator.pushView(View_DeselectAllLessons, null, null, transition_SlideView_Left);
               break;
            }
            case _LIST_OPTION__SELECT_LESSONS: {
               if (model.getLessonVersionCount() < 1) {
                  var message:String = Utils_LangCollab.getMessage_NoLessonsDownloaded();
                  Utils_ANEs.showAlert_OkayButton(message);
                  return;
               }
               if (appStatePersistenceManager.retrieveIsHelp_LastViewDate_SelectLessons_Saved()) {
                  ctxt = new ViewContext(ViewContext.CONTEXT_TYPE__MANAGE_LESSONS);
                  navigator.pushView(View_SelectLessons, null, ctxt, transition_SlideView_Left);
               }
               else {
                  ctxt = new ViewContext(ViewContext.CONTEXT_TYPE__HELP);
                  ctxt.nextViewClass = View_SelectLessons;
                  navigator.pushView(View_Help_Select_Lessons, null, ctxt, transition_SlideView_Left);
               }
               break;
            }
            case _LIST_OPTION__SELECT_LESSON_LEVELS: {
               ctxt = new ViewContext(ViewContext.CONTEXT_TYPE__MANAGE_LESSONS);
               navigator.pushView(View_LessonLevels, null, ctxt, transition_SlideView_Left);
               break;
            }
            default: {
               Log.fatal("View_ManageLessons.onListChange(): No case for " + list.selectedItem);
            }
         }
      }

      private function onViewActivate():void {
         _listDataProvider = new ArrayCollection();
         _listDataProvider.addItem(_LIST_OPTION__DELETE_I_KNOW_THIS_DATA);
         _listDataProvider.addItem(_LIST_OPTION__DESELECT_ALL_LESSONS);
         _listDataProvider.addItem(_LIST_OPTION__SELECT_LESSONS);
         _listDataProvider.addItem(_LIST_OPTION__SELECT_LESSON_LEVELS);
         list.dataProvider = _listDataProvider;
      }
      ]]>
    </fx:Script>

   <list:NormalList id = "list"
                    change = "onListChange()"
                    itemRenderer = "com.brightworks.component.itemrenderer.BwLabelItemRenderer"
                    height = "100%"
                    width = "100%"/>
</view:View_Base>
