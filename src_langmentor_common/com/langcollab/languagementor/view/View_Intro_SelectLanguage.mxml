<?xml version = "1.0" encoding = "utf-8"?>
<!--
Copyright 2018 Brightworks, Inc.

This file is part of Language Mentor.

Language Mentor is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

Language Mentor is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Language Mentor.  If not, see <http://www.gnu.org/licenses/>.
-->
<view:View_Intro_Base xmlns:fx = "http://ns.adobe.com/mxml/2009"
                      xmlns:view = "com.langcollab.languagementor.view.*"
                      creationComplete = "onCreationComplete()"
                      title = "Intro &amp; Setup: Select Language">

   <fx:Declarations>
   </fx:Declarations>

   <fx:Script>
      <![CDATA[
      import com.brightworks.component.checkbox.NormalCheckBox;
      import com.brightworks.component.list.NormalList;
      import com.brightworks.util.Log;
      import com.brightworks.util.Utils_Text;
      import com.langcollab.languagementor.component.itemrenderer.ItemRenderer_LanguageSelectionList;
      import com.langcollab.languagementor.component.uiwidget.IUIWidget_Text;
      import com.langcollab.languagementor.component.uiwidget.UIWidget_Viewlet__Heading;

      import mx.collections.ArrayCollection;
      import mx.collections.Sort;
      import mx.collections.SortField;
      import mx.controls.Spacer;

      import spark.components.BorderContainer;
      import spark.components.List;

      private var _displayOnlyLanguagesWithRecommendedLibrariesCheckbox:NormalCheckBox;
      private var _isDisposed:Boolean = false;
      private var _languageList:List;
      private var _languageListDataProvider:ArrayCollection = new ArrayCollection();

      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      //
      //          Public Methods
      //
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

      override public function dispose():void {
         super.dispose();
         if (_isDisposed)
            return;
         _isDisposed = true;
         _languageList.removeEventListener(Event.CHANGE, onListChange);
      }

      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      //
      //          Protected Methods
      //
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

      override protected function onNextButtonClick(event:MouseEvent):void {
         Log.info("View_Intro_SelectLanguage.onContinueButtonClick()");
         if (!_languageList.selectedItem) {
            Log.warn("View_Intro_SelectLanguage.onContinueButtonClick(): no selectedItem");
            return;
         }
         model.initTargetLanguage(_languageList.selectedItem["value"], true);
         super.onNextButtonClick(event);
      }

      override protected function onViewActivate():void {
         super.onViewActivate();
         nextButton.enabled = false;
         populateLanguageListDataProvider();
      }

      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      //
      //          Private Methods
      //
      // - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

      private function onCreationComplete():void {
         var textWidget:IUIWidget_Text;
         var borderContainer:BorderContainer;
         textWidget = new UIWidget_Viewlet__Heading();
         textWidget.text = "What language would you like to learn?";
         _displayOnlyLanguagesWithRecommendedLibrariesCheckbox = new NormalCheckBox();
         _displayOnlyLanguagesWithRecommendedLibrariesCheckbox.label = "Display Only Languages\nwith Recommended Libraries";
         _displayOnlyLanguagesWithRecommendedLibrariesCheckbox.selected = true;
         _displayOnlyLanguagesWithRecommendedLibrariesCheckbox.addEventListener(Event.CHANGE, onDisplayOnlyLanguagesWithRecommendedLibrariesCheckboxChange)
         viewletGroup.addElement(textWidget);
         var spacer:Spacer;
         spacer = new Spacer();
         spacer.height = Utils_Text.getStandardFontSize();
         viewletGroup.addElement(spacer);
         viewletGroup.addElement(_displayOnlyLanguagesWithRecommendedLibrariesCheckbox);
         spacer = new Spacer();
         spacer.height = Utils_Text.getStandardFontSize();
         viewletGroup.addElement(spacer);
         borderContainer = new BorderContainer();
         borderContainer.percentHeight = 100;
         borderContainer.percentWidth = 100;
         _languageList = new NormalList();
         _languageList.percentHeight = 100;
         _languageList.percentWidth = 100;
         _languageList.dataProvider = _languageListDataProvider;
         _languageList.addEventListener(Event.CHANGE, onListChange);
         _languageList.itemRenderer = new ClassFactory(ItemRenderer_LanguageSelectionList);

         borderContainer.addElement(_languageList);
         viewletGroup.addElement(borderContainer);
      }

      private function onDisplayOnlyLanguagesWithRecommendedLibrariesCheckboxChange(e:Event):void {
         populateLanguageListDataProvider();
      }

      private function onListChange(e:Event):void {
         nextButton.enabled = (_languageList.selectedItem);
      }

      private function populateLanguageListDataProvider():void {
         _languageListDataProvider.removeAll();
         var sort:Sort = new Sort();
         sort.fields = [new SortField("label")];
         _languageListDataProvider.sort = sort;
         _languageListDataProvider.refresh();
         var languageIdList:Array = model.getLanguageIdList();
         for each (var id:int in languageIdList) {
            if (_displayOnlyLanguagesWithRecommendedLibrariesCheckbox.selected) {
               var iso639_3Code:String = model.getLanguageIso639_3CodeFromId(id);
               if (!model.doesTargetLanguageHaveRecommendedLibraries(iso639_3Code))
                     continue;
            }
            var displayName:String = model.getLanguageDisplayName_Alphabetizable_InCurrentNativeLanguage(id);
            var item:Object = {label: displayName, value: id};
            _languageListDataProvider.addItem(item);
         }
      }
      ]]>
   </fx:Script>
</view:View_Intro_Base>
